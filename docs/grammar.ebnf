pgn         ::= tag_section movetext_section
tag_section ::= tag_pair*
tag_pair    ::= "[" identifier string "]"

movetext_section ::= (element | result)*
element    ::= move_number | move | comment | rav
move_number::= digit+ "."

move             ::= ( (pawn_capture | piece_capture) | (pawn_move | piece_move) | castling ) check? nag?
pawn_move        ::= destination promotion?
piece_move       ::= piece disambiguation? destination
pawn_capture     ::= file "x" destination promotion?
piece_capture    ::= piece disambiguation? "x" destination
comment          ::= "{" [^}]+ "}" | ";" [^\n]*
rav              ::= "(" (element | result)+ ")"

check            ::= "+" | "#"
nag              ::= "$" digit+
piece            ::= "N" | "B" | "R" | "Q" | "K"
destination      ::= file rank
disambiguation   ::= file | rank
promotion        ::= "=" piece
castling         ::= "O-O" | "O-O-O"
file             ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h"
rank             ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8"
result           ::= "*" | "1-0" | "0-1" | "1/2-1/2"

identifier       ::= [A-Za-z_]+
string           ::= "\"" [^"]* "\""
